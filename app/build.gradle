apply plugin: 'com.android.application'

String umengAppKey = System.getenv('CNODEMD_UMENG_APP_KEY')
File localPropertiesFile = project.rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    Properties properties = new Properties()
    properties.load(localPropertiesFile.newDataInputStream())
    umengAppKey = properties.getProperty('umeng.appkey', umengAppKey)
}

android {

    compileSdkVersion 26

    defaultConfig {
        applicationId 'org.cnodejs.android.md'
        minSdkVersion 14
        targetSdkVersion 26
        versionCode Integer.parseInt(new Date().format('yyyyMMdd'))
        versionName '1.4.1-dev.1'

        manifestPlaceholders = [
                UMENG_APPKEY: umengAppKey,
                UMENG_CHANNEL: 'CNodeJS'
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'net.danlew:android.joda:2.9.9'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    implementation 'com.takwolf.android:hf-recyclerview:0.2.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.2'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'

    implementation 'org.tautua.markdownpapers:markdownpapers-core:1.4.4'
    implementation 'org.jsoup:jsoup:1.10.3'

    implementation 'org.cnodejs.android:oauth-login:0.1.0'

    implementation 'com.umeng.analytics:analytics:6.1.2'
}
